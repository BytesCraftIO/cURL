plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8
group 'io.bytescraft'
archivesBaseName = 'cURL'
version '0.0.3'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.auto.service:auto-service:1.1.1'
    implementation 'com.javaquery:util:1.2.4'
    implementation 'org.json:json:20240303'
    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                'Built-By': "BytesCraft Team"
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.bytescraft'
            artifactId = 'cURL'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'cURL'
                description = 'cURL Annotation for Spring project to generate HTTP request documents'
                url = 'https://github.com/BytesCraftIO/cURL'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/BytesCraftIO/cURL/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'BytesCraftIO'
                        name = 'BytesCraftIO Team'
                        email = 'admin@bytescraft.com'
                        timezone = '+05:30'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/BytesCraftIO/cURL.git'
                    developerConnection = 'scm:git:ssh://github.com/BytesCraftIO/cURL.git'
                    url = 'https://github.com/BytesCraftIO/cURL'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}